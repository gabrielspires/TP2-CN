set term pdf
set output 'plots/ttree.pdf'

set xr [0:50]   
set yr [0:50]

#set size square   

flePnts = 'dados/pnts.dat'
fleEdges = 'dados/edges.dat'

set style line 1 lc rgb '#8b1a0e' pt 7 ps 0.3 lt 1 lw 2 # --- red
set style line 2 lc rgb '#1E90FF' pt 7 ps 0.3 lt 1 lw 2 # --- blue
set style line 3 lc rgb '#45CE30' pt 7 ps 0.3 lt 1 lw 2 # --- green
set style line 11 lc rgb '#808080' lt 1
set style line 12 lc rgb '#808080' lt 0 lw 1
set border 3 back ls 11
set tics nomirror 
set grid back ls 12
set format x ""
set format y ""
set nokey

loadEdges = sprintf('< gawk '' \
    FNR==NR{x[$1]=$2;y[$1]=$3;next;} \
    {printf "%%f\t%%f\n%%f\t%%f\n\n", x[$1], y[$1], x[$2], y[$2];} \
'' %s %s', flePnts, fleEdges); 

loadWeights = sprintf('< gawk '' \
    FNR==NR{x[$1]=$2;y[$1]=$3;next;} \
    {printf "%%f\t%%f\t%%s\n", (x[$1]+x[$2])/2 + $4, (y[$1]+y[$2])/2 + $5, $3} \
'' %s %s', flePnts, fleEdges);

plot \
    loadEdges using 1:2 with lines lc rgb "black" lw 2 notitle, \
    flePnts using 2:3:(0.6) with circles fill solid lc rgb "black" notitle, \
    flePnts using 2:3:1 with labels tc rgb "white" font "Arial Bold,7" notitle, \
    loadWeights using 1:2:3 with labels tc rgb "red" center font "Arial Bold,7" notitle